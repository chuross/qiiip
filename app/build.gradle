apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.tmiyamon.config'
apply plugin: 'deploygate'

buildscript {
    def v = rootProject.ext.versions
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$v.kotlin"
        classpath 'com.deploygate:gradle:1.1.5'
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.2"
    defaultConfig {
        applicationId "com.github.chuross.qiiip"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

deploygate {
    userName = rootProject.ext.deployGate.userName
    token = rootProject.ext.deployGate.token

    apks {
        debug {
            distributionKey = "5487db9073b30266c79ccb12b630fdc1f01bc23b"
            sourceFile = file("${project.rootDir}/app/build/outputs/apk/app-debug.apk")
        }
    }
}

def v = rootProject.ext.versions
dependencies {
    // base
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$v.kotlin"
    implementation "org.apache.commons:commons-lang3:$v.commonsLang"
    implementation "com.jakewharton.timber:timber:$v.timber"
    implementation "com.squareup.okhttp3:logging-interceptor:$v.okhttp"
    implementation "com.squareup.retrofit2:retrofit:$v.retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$v.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$v.retrofit"
    implementation "com.google.dagger:dagger:$v.dagger"
    kapt "com.google.dagger:dagger-compiler:$v.dagger"
    implementation "androidx.lifecycle:lifecycle-extensions:$v.androidxArch"
    implementation "com.github.chuross.viewmodelargs:annotation:$v.viewModelArgs"
    kapt "com.github.chuross.viewmodelargs:compiler:$v.viewModelArgs"

    // store
    implementation "com.scottyab:secure-preferences-lib:$v.securePref"
    implementation "com.rejasupotaro:kvs-schema:$v.kvsSchema"
    kapt("com.rejasupotaro:kvs-schema-compiler:$v.kvsSchema") {
        exclude group: 'com.google.android', module: 'android'
    }

    // rx
    implementation "io.reactivex.rxjava2:rxjava:$v.rx"
    implementation "io.reactivex.rxjava2:rxandroid:$v.rx"
    implementation "com.trello.rxlifecycle2:rxlifecycle:$v.rx"
    implementation "com.trello.rxlifecycle2:rxlifecycle-android:$v.rx"
    implementation "com.trello.rxlifecycle2:rxlifecycle-kotlin:$v.rx"
    implementation "com.github.MFlisar:RxBus2:$v.rxbus"
    implementation "com.github.k-kagurazaka.rx-property-android:rx-property:$v.rxProperty"
    implementation "com.github.k-kagurazaka.rx-property-android:rx-property-kotlin:$v.rxProperty"

    // base view
    kapt "com.android.databinding:compiler:$v.databinding"
    implementation "androidx.appcompat:appcompat:$v.androidxCore"
    implementation "androidx.fragment:fragment:$v.androidxCore"
    implementation "androidx.recyclerview:recyclerview:$v.androidxCore"
    implementation "androidx.cardview:cardview:$v.androidxCore"
    implementation "com.google.android.material:material:$v.androidxCore"

    // view
    implementation "com.github.chuross.mori-router:annotation:$v.moriRouter"
    kapt "com.github.chuross.mori-router:compiler:$v.moriRouter"
    implementation "com.github.chuross.recyclerview-adapters:recyclerview-adapters:$v.recyclerViewAdapters"
    implementation "com.github.chuross.recyclerview-adapters:recyclerview-adapters-rx:$v.recyclerViewAdapters"
    implementation "me.gujun.android.taggroup:library:$v.tagGroup@aar"
    implementation "com.zzhoujay.richtext:richtext:$v.richText"

    //image
    implementation "com.squareup.picasso:picasso:$v.picasso"
    implementation "jp.wasabeef:picasso-transformations:$v.picassoTransformer"
    implementation "com.jakewharton.picasso:picasso2-okhttp3-downloader:$v.picassoOkhttp3"

    // test
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
    testImplementation 'junit:junit:4.12'
}
